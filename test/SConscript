# SConscript

Import("env")


def test_kat(name, kat):
    prog = env.Program(name + ".c")
    out = env.Command(target=name + ".txt", source=prog, action="$SOURCE > $TARGET")
    env.Command(
        target=name + ".diff",
        source=[out, kat],
        # Strip trailing CRs for comparing against files generated on Windows.
        action="diff --strip-trailing-cr ${SOURCES[0]} ${SOURCES[1]}",
    )


test_kat("test_gimli", "test_gimli.exp")
test_kat("test_lwc_hash_kat", "LWC_HASH_KAT_256.txt")
test_kat("test_lwc_aead_kat", "LWC_AEAD_KAT_256_128.txt")


def test(name, extra_sources=[]):
    prog = env.Program(target=name, source=[name + ".c"] + extra_sources)
    env.Command(target=name + ".txt", source=prog, action="$SOURCE ")


env_ed25519 = env.Clone()
env_ed25519.Append(CCFLAGS=["-Wno-conversion", "-fwrapv"])

test("test_x25519")
test("test_fe")
test("test_reduce", extra_sources=[env_ed25519.Object("sc_reduce.c")])
